define

  # --------------------------------------
  # 0. Global Attributes & Relations
  # --------------------------------------
  # [Attributes]
  attribute id-serial-number, value string;
  attribute id-party-id, value string;
  attribute id-doc-id, value string;
  attribute id-chunk-id, value string;
  attribute id-location-id, value string;
  attribute code-error, value string;

  attribute name, value string;
  attribute description, value string;
  attribute content-text, value string;
  attribute file-type, value string;
  attribute manufacturer, value string;
  attribute severity, value string;
  attribute timestamp, value datetime;
  attribute date-action, value datetime;

  attribute email, value string;
  attribute phone, value string;
  attribute role-grade, value string;

  attribute version, value string;
  attribute language, value string;
  attribute status, value string;       # Draft, Published, Archived
  attribute row-index, value integer;      # 엑셀 행 번호
  attribute page-number, value integer;    # PDF 페이지 번호
  
  attribute created-date, value datetime;
  attribute valid-period, value string;
  attribute issuer, value string;

  # [Relations]
  relation assembly,
    relates system,
    relates part;

  relation location,
    relates located,
    relates place;

  relation monitoring,
    relates observer,
    relates target;

  relation state-change,
    relates affected-asset,
    relates defined-state,
    owns timestamp;

  relation containment,
    relates container,
    relates content;

  relation knowledge-link,
    relates source,
    relates related-hardware,
    relates related-topic;

  relation maintenance,
    relates performed-action,
    relates performer,
    relates target-asset,
    relates guide;

  relation responsibility,
    relates responsible,
    relates subject-area;

  relation authorship,
    relates author,
    relates created-content;

  relation reporting,
    relates reporter,
    relates reported-state,
    relates evidence,    
    owns timestamp;

  relation citation,
    relates source,      
    relates target;      

  relation mention,
    relates source,
    relates target;

  # --------------------------------------
  # WORLD 1: PHYSICAL WORLD (물리적 자산)
  # --------------------------------------

  # === L1: Abstract Concepts (추상적 개념) ===
  entity physical-asset,
    owns name,
    owns id-serial-number,
    owns manufacturer,
    plays location:place,
    plays location:located,
    plays state-change:affected-asset,
    plays maintenance:target-asset,
    plays mention:target,
    plays knowledge-link:related-hardware,
    plays citation:target;

  entity space,
    owns id-location-id,
    owns name,
    plays location:place,
    plays responsibility:subject-area,
    plays mention:target;

  # === L2: Industry Standards (산업 공통 분류) ===
  entity equipment,
    sub physical-asset,
    plays assembly:system;

  entity component,
    sub physical-asset,
    plays assembly:part;

  entity sensor,
    sub physical-asset,
    plays monitoring:observer;

  entity site, sub space;
  entity zone, sub space;

  # === L3: Company Specifics (개별 기업/현장 구체화) ===
  # 예시: 특정 회사의 설비 유형
  # entity pump, sub equipment;
  # entity motor, sub component;
  entity unit, sub equipment;

  # --------------------------------------
  # WORLD 2: DOCUMENT WORLD (지식 자산)
  # --------------------------------------

  # === L1: Abstract Concepts ===
  entity knowledge-resource,
    owns name,
    owns created-date,
    plays knowledge-link:source,
    plays maintenance:guide,
    plays authorship:created-content;

  # === L2: Structure & Format (형식적 분류) ===
  entity document-file,
    sub knowledge-resource,
    owns id-doc-id,
    owns file-type,
    owns version,
    owns language,
    owns status,
    plays containment:container,
    plays citation:source,
    plays citation:target;

  entity content-unit,
    sub knowledge-resource,
    owns id-chunk-id,
    owns content-text,
    plays containment:content,
    plays mention:source;

  # === L3: Business Context (비즈니스 목적별 분류) ===
  
  # 1. 지침서 (Manual, SOP)
  entity manual,
    sub document-file;

  # 2. 보고서 (Report, Log)
  entity report,
    sub document-file,
    plays reporting:evidence;

  # 3. 규격서 (Spec)
  entity specification,
    sub document-file,
    owns valid-period;

  # 4. 외부 표준 (Standard)
  entity standard,
    sub document-file,
    owns issuer;

  # 5. 청크 타입 (데이터 형태)
  entity text-chunk,
    sub content-unit,
    owns page-number;

  entity table-row,
    sub content-unit,
    owns row-index;

  # --------------------------------------
  # WORLD 3: ACTORS & EVENTS (주체 및 행위)
  # --------------------------------------

  # === L1 ===
  entity party,
    owns id-party-id,
    owns name,
    owns email,
    owns phone,
    plays responsibility:responsible,
    plays maintenance:performer,
    plays reporting:reporter,
    plays authorship:author,
    plays mention:target;

  entity operational-state,
    owns name,
    owns code-error,
    owns description,
    owns severity,
    plays state-change:defined-state,
    plays reporting:reported-state,
    plays knowledge-link:related-topic,
    plays mention:target;

  entity action,
    owns name,
    owns date-action,
    plays maintenance:performed-action,
    plays mention:target;

  # === L2 ===
  entity engineer,
    sub party,
    owns role-grade;

  entity operator,
    sub party;

  entity manager,
    sub party;

  entity department,
    sub party;
  

  entity fault, sub operational-state;
  entity alarm, sub operational-state;
  entity maintenance-activity, sub action;

  
  # === L3 ===